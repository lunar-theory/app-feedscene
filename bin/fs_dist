#!/usr/bin/env perl -w

use 5.12.0;
use lib 'lib';
use App::FeedScene::Distributor;
use Getopt::Long;
Getopt::Long::Configure( qw(bundling) );

GetOptions(
    'app|a=s'        => \my $app,
    'bucket|b=s' => \my $bucket,
    'verbose|V+' => \my $verbose,
    'help|h'     => \my $help,
    'man|m'      => \my $man,
    'version|v'  => \my $version,
) or _pod2usage();

_pod2usage(
    ( $man ? ( '-sections' => '.+' ) : ()),
    '-exitval' => 0,
) if $help or $man;

# Handle version request.
if ($version) {
    require File::Basename;
    say File::Basename::basename($0), ' ', App::FeedScene->VERSION;
    exit;
}

_pod2usage('-message' => 'Missing or invalid --app option') unless $app;

App::FeedScene::Distributor->new(
    app     => $app,
    bucket  => $bucket || 'feedscene',
    verbose => $verbose,
)->run;

sub _pod2usage {
    require Pod::Usage;
    Pod::Usage::pod2usage(
        '-verbose'  => 99,
        '-sections' => '(?i:(Usage|Options))',
        '-exitval'  => 1,
        @_
    );
}

=head1 Name

fs_dist - FeedScene feed distributor

=head1 Usage

  fs_dist --app DesignScene --bucket feedscene

=head1 Description

This program compresses and uploads an Atom feed to an Amazon S3 bucket, from
which it can be served via CloudFront.

=head1 Options

  -a --app NAME       Name of the FeedScene app.
  -b --bucket BUCKET  S3 bucket to which the file should be uploaded
  -V --verbose        Incremental verbosity to STDERR.
  -h --help           Print a usage statement and exit.
  -m --man            Print the complete documentation and exit.
  -v --version        Print the version number and exit.

=head1 Author

David E. Wheeler <david@lunar-theory.com>

=head1 Copyright

Copyright (c) 2010 David E. Wheeler. All rights reserved.

=cut
