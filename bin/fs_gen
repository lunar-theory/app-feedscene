#!/usr/bin/env perl -w

use 5.12.0;

use lib 'lib';
use App::FeedScene::Generator;
use Getopt::Long;
Getopt::Long::Configure( qw(bundling) );

GetOptions(
    'app|a=s'        => \my $app,
    'strict|s'       => \my $strict,
    'images-only|i'  => \my $images_only,
    'limit|l=i'      => \my $limit,
    'text-limit|t=i' => \my $text_limit,
#    'verbose|V+'     => \my $verbose,
    'help|h'         => \my $help,
    'man|m'          => \my $man,
    'version|v'      => \my $version,
) or _pod2usage();

_pod2usage(
    ( $man ? ( '-sections' => '.+' ) : ()),
    '-exitval' => 0,
) if $help or $man;

# Handle version request.
if ($version) {
    require File::Basename;
    say File::Basename::basename($0), ' ', App::FeedScene->VERSION;
    exit;
}

_pod2usage('-message' => 'Missing or invalid --app option') unless $app;

App::FeedScene::Generator->new(
    app         => $app,
    strict      => $strict,
    images_only => $images_only,
    (defined $limit      ? (limit      => $limit     ) : ()),
    (defined $text_limit ? (text_limit => $text_limit) : ()),
)->go;

sub _pod2usage {
    require Pod::Usage;
    Pod::Usage::pod2usage(
        '-verbose'  => 99,
        '-sections' => '(?i:(Usage|Options))',
        '-exitval'  => 1,
        @_
    );
}

=head1 Name

fs_gen - FeedScene feed generator

=head1 Usage

  fs_gen --app APPNAME

=head1 Description

This program generates a feed for a FeedScene app by selecting the appropriate
number of entries from the database and writing to an Atom feed file.

=head1 Options

  -a --app NAME         Name of the FeedScene app.
  -l --limit LIMIT      Number of entries to output in each non-text portal.
  -t --text-limit LIMIT Number of entries to output in the text portal.
  -i --images-only      Output enclosures only if they're images.
  -s --strict           Output strictly compliant Atom feed.
  -h --help             Print a usage statement and exit.
  -m --man              Print the complete documentation and exit.
  -v --version          Print the version number and exit.

=head1 Author

David E. Wheeler <david@lunar-theory.com>

=head1 Copyright

Copyright (c) 2010 David E. Wheeler. All rights reserved.

=cut
