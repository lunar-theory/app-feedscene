#!/usr/bin/env perl -w

use 5.12.0;

use lib 'lib';
use App::FeedScene::DBA;
use Getopt::Long;
Getopt::Long::Configure( qw(bundling) );

GetOptions(
    'app|a=s'    => \my $app,
    'client|c=s' => \my $client,
    'dir|d=s'    => \my $dir,
#   'verbose|V+' => \my $verbose,
    'help|h'     => \my $help,
    'man|m'      => \my $man,
    'version|v'  => \my $version,
) or _pod2usage();

_pod2usage(
    ( $man ? ( '-sections' => '.+' ) : ()),
    '-exitval' => 0,
) if $help or $man;

# Handle version request.
if ($version) {
    require File::Basename;
    say File::Basename::basename($0), ' ', App::FeedScene->VERSION;
    exit;
}

_pod2usage('-message' => 'Missing or invalid --app option') unless $app;

my $command = shift or _pod2usage( '-message' => 'Missing command' );
my $meth = App::FeedScene::DBA->can($command)
    or _pod2usage('-message' => qq{Invalid command: "$command"});

App::FeedScene::DBA->new(
    app     => $app,
    client  => $client,
    sql_dir => $dir,
)->$meth;

sub _pod2usage {
    require Pod::Usage;
    Pod::Usage::pod2usage(
        '-verbose'  => 99,
        '-sections' => '(?i:(Usage|Options))',
        '-exitval'  => 1,
        @_
    );
}

=head1 Name

fs_dba - FeedScene database administration

=head1 Usage

  fs_dba --app APPNAME COMMAND

=head1 Description

This program initializes and updates FeedScene application SQLite databases.
The databases are stored in the F<db> directory, and each is named for the
C<--name> option.

=head1 Options

  -a --app NAME       Name of the FeedScene app.
  -c --client CLIENT  Path to the SQLite 3 client.
  -d --dir DIRECTORY  Directory containing SQL upgrade scripts
  -h --help           Print a usage statement and exit.
  -m --man            Print the complete documentation and exit.
  -v --version        Print the version number and exit.

=head1 Author

David E. Wheeler <david@kineticode.com>

=head1 Copyright

Copyright (c) 2010 David E. Wheeler. All rights reserved.

=cut
