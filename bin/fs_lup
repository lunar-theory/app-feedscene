#!/usr/bin/env perl -w

use 5.12.0;

use lib 'lib';
use App::FeedScene::LinkUpdater;
use Getopt::Long;
Getopt::Long::Configure( qw(bundling) );

GetOptions(
    'app|a=s'    => \my $app,
    'url|u=s'    => \my $url,
#   'verbose|V+' => \my $verbose,
    'help|h'     => \my $help,
    'man|m'      => \my $man,
    'version|v'  => \my $version,
) or _pod2usage();

_pod2usage(
    ( $man ? ( '-sections' => '.+' ) : ()),
    '-exitval' => 0,
) if $help or $man;

# Handle version request.
if ($version) {
    require File::Basename;
    say File::Basename::basename($0), ' ', App::FeedScene->VERSION;
    exit;
}

_pod2usage('-message' => 'Missing or invalid --app option') unless $app;
_pod2usage('-message' => 'Missing or invalid --url option') unless $url;

App::FeedScene::LinkUpdater->new(
    app  => $app,
    url  => $url
)->run;

sub _pod2usage {
    require Pod::Usage;
    Pod::Usage::pod2usage(
        '-verbose'  => 99,
        '-sections' => '(?i:(Usage|Options))',
        '-exitval'  => 1,
        @_
    );
}

=head1 Name

fs_lup - FeedScene link updater

=head1 Usage

  fs_lup --app APPNAME --url URL

=head1 Description

This program downloads a CSV file from a URL, parses it, and updates the
C<links> table in the app database. It should be regularly run from a cron job
to keep the list of feed URLs for an app up-to-date.

=head1 Options

  -a --app NAME       Name of the FeedScene app.
  -u --url URL        URL for a CSV file
  -h --help           Print a usage statement and exit.
  -m --man            Print the complete documentation and exit.
  -v --version        Print the version number and exit.

=head1 Author

David E. Wheeler <david@kineticode.com>

=head1 Copyright

Copyright (c) 2010 David E. Wheeler. All rights reserved.

=cut
